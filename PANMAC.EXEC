/**PANMAC - Create panel MACRO file from PANEL file.       12/10/90  *          
*                                                                    *          
*  Use PANMAC to pre-process a PANEL file and create a MACRO file    *          
*  that contains the pre-processed panel definition.                 *          
*                                                                    *          
*                PANMAC disk name                                    *          
*                       |    |                                       *          
*                       |    |_ Filename of PANEL file.              *          
*                       |       If name is not specified, all PANEL  *          
*                       |       files on the disk will be processed. *          
*                       |                                            *          
*                       |_ Virtual address of the disk on which the  *          
*                          files to be processed are located.        *          
*                                                                    *          
* - - COPYRIGHT 12/22/90 - CENTRAL SYSTEMS, INC. - DALLAS, TEXAS - - *          
**********************************************************************          
*0* 00/00/00                                                         *          
*********************************************************************/          
TRACE 'O'                                                                       
Arg disk name                                                                   
Parse source . . $ .                                                            
   if disk='' then do                                                           
     say 'DISK VIRTUAL ADDRESS MUST BE SPECIFIED - '$' TERMINATED'              
     signal end                                                                 
   end                                                                          
/* See if specified disk is already access R/W; if so, use same mode */         
   ml=''                                                                        
  'SADT *'                                                                      
   do queued()                                                                  
     parse pull . . . addr md . . . . rw                                        
     if addr=disk & rw='R/W' then ml=md                                         
   end                                                                          
/* Get first available file mode letter if needed  */                           
   if ml='' then do                                                             
    'GETFMADR'                                                                  
     pull . ml .                                                                
     newmode='Y'                                                                
   end                                                                          
   if newmode='Y' then 'ACCESS 'disk ml                                         
   if rc /= 0 then do                                                           
     say 'ERROR ON ACCESS FOR DISK 'disk' - '$' TERMINATED'                     
     signal end                                                                 
   end                                                                          
   if name='' then do                                                           
     say 'NO NAME SPECFIED; ALL PANEL FILES ON DISK 'disk' WILL BE PROCESSED.'  
     say 'Enter ALL to continue.'                                               
     pull answer                                                                
     if answer/='ALL' then signal end                                           
     name='*'                                                                   
   end                                                                          
  'FDELETE PANMAC MACLIB 'ml                                                    
/* Copy the following lines by using value of sourceline() and sigl    */       
   signal textend                                                               
   /* $PANMAC EXEC */                                                           
   arg disk ml names                                                            
   i=0                                                                          
   do until names=''                                                            
     i=i+1                                                                      
     parse var names nam.i names                                                
   end                                                                          
   do j=1 to i                                                                  
     if exist(nam.j' PANEL 'ml)=1 then do                                       
       address ISPEXEC 'SELECT PGM(ISPPREP)',                                   
              'PARM(INPAN('nam.j' PANEL 'ml'),',                                
              'OUTPAN(PANMAC MACLIB =),REPLACE) NEWAPPL'                        
       if rc=0 | nam.j='*' then install='YES'                                   
       else if nam.j/='*' then do                                               
         say nam.j 'MACRO NOT CREATED; ISPPREP RC='rc'.'                        
         if rc=4 then say copies(' ',length(nam.j)),                            
        'PANEL DEFINITION VIOLATES RESTRICTIONS FOR PREPROCESSED PANELS.'       
         if rc=8 then say copies(' ',length(nam.j)),                            
        'SYNTAX ERROR IN PANEL DEFINITION.'                                     
       end                                                                      
     end                                                                        
     else do                                                                    
       say nam.j 'MACRO NOT CREATED.'                                           
       say copies(' ',length(nam.j)),                                           
      'PANEL FILE NOT FOUND ON DISK 'disk'.'                                    
     end                                                                        
   end                                                                          
   paname=''                                                                    
   If install='YES' then do until rc/=0                                         
    'EXECIO 1 DISKR PANMAC MACLIB 'ml' (VAR LINE'                               
     If rc=0 then do                                                            
       Select                                                                   
         When left(line,8)='2323'x'PREP: ' then do                              
           Parse var line . paname .                                            
          'FDELETE 'paname' PANMAC 'ml                                          
          'EXECIO 1 DISKW 'paname' PANMAC 'ml' 0 F 80 (VAR LINE'                
         End                                                                    
         When left(line,4)='61FFFF61'x then do                                  
          'EXECIO 1 DISKW 'paname' PANMAC 'ml' 0 F 80 (FINIS VAR LINE'          
           paname=''                                                            
         End                                                                    
         Otherwise if paname/='' then do                                        
          'EXECIO 1 DISKW 'paname' PANMAC 'ml' 0 F 80 (VAR LINE'                
         End                                                                    
       End                                                                      
     End                                                                        
   End                                                                          
  'ERASE PANMAC MACLIB 'ml                                                      
   exit 4                                                                       
textend:                                                                        
     startcopy=sigl+1                                                           
     signal copy                                                                
copy:                                                                           
     endcopy=sigl-3                                                             
     do i=startcopy to endcopy                                                  
       line=sourceline(i)                                                       
       call write                                                               
     end                                                                        
    'CSISPF CMD($PANMAC 'disk ml name') CMS'                                    
end:                                                                            
stop:                                                                           
   if newmode='Y' then 'RELEASE 'ml                                             
exit                                                                            
write:                                                                          
  'EXECIO 1 DISKW $PANMAC EXEC A3 (FINIS STRING' line                           
return                                                                          
